plugins {
    // see https://fabricmc.net/develop/ for new versions
    id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    // see https://projects.neoforged.net/neoforged/moddevgradle for new versions
    id 'net.neoforged.moddev' version '0.1.110' apply false
}

mkdir file("build/tmp/javadoc/collect")
mkdir file("build/docs/javadoc")

evaluationDependsOn(":common")

tasks.register("javadoc", Copy) {
    dependsOn "common:javadoc"

    doFirst {
        mkdir file("build/docs/javadoc")
    }

    from(project(":common").javadoc.outputs)
    into(file("build/docs/javadoc"))
}

tasks.register("jar", Copy) {
    dependsOn "common:build"
    dependsOn "neoforge:build"
    dependsOn "fabric:build"

    doFirst {
        mkdir file("build/libs")
    }

    from(project(":common").jar.outputs)
    from(project(":neoforge").jar.outputs)
    from(project(":fabric").jar.outputs)
    into(file("build/libs"))
}

tasks.register("build") {
    dependsOn "javadoc"
    dependsOn "jar"
}
