plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.ultreon'
version = '0.1.0-SNAPSHOT'

base {
    archivesName = 'vefi'
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.jetbrains:annotations:26.0.1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Extra dependencies
    api 'org.ow2.asm:asm-tree:9.7'
    api 'org.ow2.asm:asm:9.7'
    api 'org.ow2.asm:asm-commons:9.7'
    api 'org.ow2.asm:asm-util:9.7'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            name = "UltreonMavenReleases"
            url = "https://maven.ultreon.dev/releases"
            credentials(PasswordCredentials) {
                username = findProperty("ultreonmvn.name") ?: System.getenv("ULTREON_MVN_NAME")
                password = findProperty("ultreonmvn.secret") ?: System.getenv("ULTREON_MVN_SEC")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }

        maven {
            name = "UltreonMavenSnapshots"
            url = "https://maven.ultreon.dev/snapshots"
            credentials(PasswordCredentials) {
                username = findProperty("ultreonmvn.name") ?: System.getenv("ULTREON_MVN_NAME")
                password = findProperty("ultreonmvn.secret") ?: System.getenv("ULTREON_MVN_SEC")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
